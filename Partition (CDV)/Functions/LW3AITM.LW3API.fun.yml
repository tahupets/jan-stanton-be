Function:
  Enabled For RDMLX:  No

  Source: |
    * =======================================================
    * Process Name - LW3API
    * Process Description - Back Office file API's
    
    * Function Name - LW3AITM
    * Function Description - Item master API caller
    
    * EXCHANGE FIELDS(In)
    
    * #FXACTION  - Action code - C = Check_for, G = Get item master
    * #LW3ITEMCD - Item code
    
    
    * EXCHANGE FIELDS(Out)
    
    * #LW3IDESC - Item Description
    * *FXUM     - Unit of measure
    * #FXRETCD  - Return code - OK or NR
    * #LW3DSCTXT - Item extra description
    * #UNIVPR   - Universal product code
    
    * =======================================================
    * Summary of Changes
    * =======================================================
    * CR #  !  Date   ! Description
    * -------------------------------------------------------
    *       |         |
    * =======================================================
    Function Options(*DIRECT *HEAVYUSAGE)
    * =======================================================
    
    Define Field(#W_BO) Type(*CHAR) Length(10)
    
    * =======================================================
    * Program main line
    * =======================================================
    
    Execute Subroutine(INIT)
    
    * Continue depending on which back office we interface with
    Case Of_Field(#W_BO)
    
    When Value_Is('*EQ ''ERPF''')
    Exchange Fields(#FXACTION #LW3ITEMCD #FXLNGCOD #LW3LNGBO)
    Call Process(*DIRECT) Function(LW3AITE) If_Error(*NEXT)
    
    When Value_Is('*EQ ''CE''')
    Exchange Fields(#FXACTION #LW3ITEMCD #FXLNGCOD)
    Call Process(*DIRECT) Function(LW3AITC) If_Error(*NEXT)
    
    When Value_Is('*EQ ''JDE''')
    Exchange Fields(#FXACTION #LW3ITEMCD #FXLNGCOD #LW3LNGBO)
    Call Process(*DIRECT) Function(LW3AITJ) If_Error(*NEXT)
    
    When Value_Is('*EQ ''BPCS''')
    Exchange Fields(#FXACTION #LW3ITEMCD #FXLNGCOD #LW3LNGBO)
    Call Process(*DIRECT) Function(LW3AITB) If_Error(*NEXT)
    
    Otherwise
    
    Change Field(#LW3IDESC #FXUM #LW3DSCTXT #UNIVPR) To(*NULL)
    Endcase
    
    
    Exchange Fields(#LW3IDESC #FXRETCD #FXUM #LW3DSCTXT #UNIVPR)
    
    Return
    
    
    * =======================================================
    Subroutine Name(INIT)
    
    Change Field(#FXRETCD) To(NR)
    Change Field(#FXLNGCOD) To(*LANGUAGE)
    Change Field(#LW3LNGBO) To(*BLANK)
    
    * Get back office ID from system file
    Change Field(#LW3SVNAME) To(BACK_OFFICE)
    Change Field(#LW3SVLANG) To(*BLANK)
    Fetch Fields(#LW3SVVALP) From_File(LW3SYS) With_Key(#LW3SVNAME #LW3SVLANG) Io_Error(*NEXT)
    If_Status Is(*OKAY)
    Change Field(#W_BO) To(#LW3SVVALP)
    Else
    * Default to CE
    Change Field(#W_BO) To(CE)
    Endif
    
    If Cond('#FXACTION *EQ *BLANK')
    Change Field(#FXACTION) To(G)
    Endif
    
    * If foreign language, get back-office language code
    If Cond('#FXLNGCOD *NE ENG')
    Fetch Fields(#LW3LNGBO) From_File(LW3LNG) With_Key(#FXLNGCOD) Io_Error(*NEXT) Val_Error(*NEXT)
    Endif
    Endroutine
